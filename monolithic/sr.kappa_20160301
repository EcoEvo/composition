
%agent: DNA(binding,us,ds,type~pSpaRK~pSpaRKtss~RBS1~spaK~RBS2~spaR~Ter1~pSpaS~pSpaStss~RBS3~gfp~Ter2)
%agent: RNA(ds,us,binding,deg,type~pSpaRKtss~RBS1~spaK~RBS2~spaR~Ter1~pSpaStss~RBS3~gfp~Ter2)
%agent: P(ds,us, binding, deg, type~pSpaRKtss~RBS1~spaK~RBS2~spaR~Ter1~pSpaStss~RBS3~gfp~Ter2)
%agent: SlideDegRule()
%agent: RNADegRule()

%agent: RNAP(rna,dna)
%agent: Ribosome(rna, protein)
%agent: Protease(binding)
%agent: RNADeg(binding)

%init: 1000 RNAP(dna,rna)

%init: 10000 Ribosome(rna)

%init: 1000 Protease(binding)
%init: 100 RNADeg(binding)


%obs:'RNA1' RNA(binding, ds!1, type~pSpaRKtss), RNA(binding, us!1, type~RBS1)
%obs:'RNA2' RNA(binding, ds!1, type~pSpaRKtss), RNA(binding, us!1,ds!2, type~RBS1), RNA(binding, us!2, type~spaK)
%obs:'RNA3' RNA(binding, ds!1, type~pSpaRKtss), RNA(binding, us!1,ds!2, type~RBS1), RNA(binding, us!2,ds!3, type~spaK), RNA(binding, us!3, type~RBS2)
%obs:'RNA4' RNA(binding, ds!1, type~pSpaRKtss), RNA(binding, us!1,ds!2, type~RBS1), RNA(binding, us!2,ds!3, type~spaK), RNA(binding, us!3,ds!4, type~RBS2), RNA(binding, us!4, type~spaR)

%obs:'SpaK' P(binding, type~spaK)
%obs:'SpaR' P(binding, type~spaR)
%obs: 'SpaK_RBS2' P(ds!1,type~spaK), P(us!1,type~RBS2)
%obs: 'SpaK_RBS2_spaR' P(ds!1,type~spaK), P(us!1,ds!2,type~RBS2),P(us!2,type~spaR)
%obs: 'SpaK_RBS2_spaR_Ter1' P(ds!1,type~spaK), P(us!1,ds!2,type~RBS2),P(us!2,ds!3,type~spaR),P(us!3,type~Ter1)
%obs:'Protein' P()
%obs: 'Protease_free' Protease(binding)
%obs:'RNADeg_free' RNADeg(binding)
%obs: 'RNAdeg' RNA(us,deg!1), RNADeg(binding!1)
%obs: 'Protease-P' P(us,ds,deg!1), Protease(binding!1)

%obs:'SlideDegRule' SlideDegRule()
%obs:'RNADegRule' RNADegRule()


#%obs: 'slideleft' RNA(us!2,ds?,deg!1), RNADeg(binding!1), RNA(ds!2, deg)
#%obs:'RNA' RNA(binding, ds!1, type~pSpaRKtss), RNA(binding, us!1,ds!2, type~RBS1), RNA(binding, us!2,ds!3, type~spaK), RNA(binding, us!3,ds!4, type~RBS2), RNA(binding, us!4, ds!5, type~spaR), RNA(binding, us!5, type~Ter1)
#%obs:'RNAP' RNAP(dna,rna)
#%obs:'RNAP-dna' RNAP(dna!_,rna)
#%obs:'RNAP-rna' RNAP(dna,rna!_)
#%obs:'SpaR' P(binding, type~spaR)
#%obs: 'Ribosome_free' Ribosome(rna,protein)
#%obs: 'Ribosome_rna' Ribosome(rna!_,protein)
#%obs: 'Ribosome_protein' Ribosome(rna,protein!_)
#%obs: 'Ribosome_protein_rna' Ribosome(rna!_,protein!_)

#%obs: 'chain1left' RNA(binding!1, ds!2, type~spaK), Ribosome(rna!1, protein!3), RNA (binding,us!2, type~RBS2), P(binding!3, type~spaK) 
#%obs: 'chain2left' RNA(binding!1, ds!2, type~RBS2), Ribosome(rna!1, protein!3), RNA (binding,us!2, type~spaR), P(binding!3, type~RBS2)
#%obs: 'chain3left' RNA(binding!1, ds!2, type~spaR), Ribosome(rna!1, protein!3), RNA (binding,us!2, type~Ter1), P(binding!3, type~spaR)

#%obs: 'left' RNA(binding, ds!2, type~spaK), Ribosome(rna!1, protein!3), RNA (binding!1,us!2, type~RBS2), P(binding!3, type~spaK) 
#%obs: 'right' RNA(binding, ds!2, type~spaK), Ribosome(rna!1, protein!3), RNA (binding!1,us!2, type~RBS2), P(ds!4, binding, type~spaK), P(type~RBS2, us!4, binding!3) 
#%obs:'RNA tss' RNA(binding,us, ds, type~pSpaRKtss)



%init: 1 DNA(binding,us,ds!1, type~pSpaRK), DNA(binding,us!1,ds!100, type~pSpaRKtss), DNA(binding, us!100, ds!2, type~RBS1), DNA(binding, us!2, ds!3, type~spaK), DNA(binding, us!3, ds!4, type~RBS2), DNA(binding, us!4, ds!5, type~spaR), DNA(binding, us!5, ds!6, type~Ter1), DNA(binding, us!6, ds!7, type~pSpaS), DNA(binding, us!7, ds!99, type~pSpaStss), DNA(binding, us!99, ds!8, type~RBS3), DNA(binding, us!8, ds!9, type~gfp), DNA(binding, us!9, ds, type~Ter2)

# RULE 1: RNAP binding to promoter region
%var: 'RNAP binding rate' 0.0001
DNA(binding, type~pSpaRK), RNAP(dna, rna) -> DNA(binding!1,type~pSpaRK), RNAP(dna!1,rna) @'RNAP binding rate'

#RULE 2: transcription initiation - corresponds to transcription of the DNA sequence of 5'-UTR and the formation of first RNA block
%var: 'transcription initiation rate' 1
'transcription initiation' DNA(binding!1,ds!2,type~pSpaRK), RNAP(dna!1,rna),DNA(us!2, binding, type~pSpaRKtss) -> DNA(binding,ds!2,type~pSpaRK), RNAP(dna!1,rna!3),DNA(us!2, binding!1, type~pSpaRKtss), RNA(binding, us, ds!3, type~pSpaRKtss)@'transcription initiation rate'

#RULE 3: Transcription sliding
%var: 'sliding rate' 1
DNA(binding,us!2,type~RBS1), RNAP(dna!1,rna!3),DNA(ds!2, binding!1, type~pSpaRKtss), RNA(ds!3, type~pSpaRKtss) -> DNA(binding!1,us!2,type~RBS1), RNAP(dna!1,rna!3),DNA(ds!2, binding, type~pSpaRKtss), RNA(ds!4, type~pSpaRKtss), RNA(binding, us!4, ds!3, type~RBS1)@'sliding rate'
DNA(binding,us!2,type~spaK), RNAP(dna!1,rna!3),DNA(ds!2, binding!1, type~RBS1), RNA(ds!3, type~RBS1) -> DNA(binding!1,us!2,type~spaK), RNAP(dna!1,rna!3),DNA(ds!2, binding, type~RBS1), RNA(ds!4, type~RBS1), RNA(binding, us!4, ds!3, type~spaK)@'sliding rate'
DNA(binding,us!2,type~RBS2), RNAP(dna!1,rna!3),DNA(ds!2, binding!1, type~spaK), RNA(ds!3, type~spaK) -> DNA(binding!1,us!2,type~RBS2), RNAP(dna!1,rna!3),DNA(ds!2, binding, type~spaK), RNA(ds!4, type~spaK), RNA(binding, us!4, ds!3, type~RBS2)@'sliding rate'
DNA(binding,us!2,type~spaR), RNAP(dna!1,rna!3),DNA(ds!2, binding!1, type~RBS2), RNA(ds!3, type~RBS2) -> DNA(binding!1,us!2,type~spaR), RNAP(dna!1,rna!3),DNA(ds!2, binding, type~RBS2), RNA(ds!4, type~RBS2), RNA(binding, us!4, ds!3, type~spaR)@'sliding rate'
DNA(binding,us!2,type~Ter1), RNAP(dna!1,rna!3),DNA(ds!2, binding!1, type~spaR), RNA(ds!3, type~spaR) -> DNA(binding!1,us!2,type~Ter1), RNAP(dna!1,rna!3),DNA(ds!2, binding, type~spaR), RNA(ds!4, type~spaR), RNA(binding, us!4, ds!3, type~Ter1)@'sliding rate'

#Without the typeof the preceeding component is much more neat but the simulation times is not reasonable
#DNA(binding,us!2,type~RBS1), RNAP(dna!1,rna!3),DNA(ds!2, binding!1), RNA(ds!3) -> DNA(binding!1,us!2,type~RBS1), RNAP(dna!1,rna!3),DNA(ds!2, binding), RNA(ds!4), RNA(binding, us!4, ds!3, type~RBS1)@'sliding rate'
#DNA(binding,us!2,type~spaK), RNAP(dna!1,rna!3),DNA(ds!2, binding!1), RNA(ds!3) -> DNA(binding!1,us!2,type~spaK), RNAP(dna!1,rna!3),DNA(ds!2, binding), RNA(ds!4), RNA(binding, us!4, ds!3, type~spaK)@'sliding rate'
#DNA(binding,us!2,type~RBS2), RNAP(dna!1,rna!3),DNA(ds!2, binding!1), RNA(ds!3) -> DNA(binding!1,us!2,type~RBS2), RNAP(dna!1,rna!3),DNA(ds!2, binding), RNA(ds!4), RNA(binding, us!4, ds!3, type~RBS2)@'sliding rate'
#DNA(binding,us!2,type~spaR), RNAP(dna!1,rna!3),DNA(ds!2, binding!1), RNA(ds!3) -> DNA(binding!1,us!2,type~spaR), RNAP(dna!1,rna!3),DNA(ds!2, binding), RNA(ds!4), RNA(binding, us!4, ds!3, type~spaR)@'sliding rate'
#DNA(binding,us!2,type~Ter1), RNAP(dna!1,rna!3),DNA(ds!2, binding!1), RNA(ds!3) -> DNA(binding!1,us!2,type~Ter1), RNAP(dna!1,rna!3),DNA(ds!2, binding), RNA(ds!4), RNA(binding, us!4, ds!3, type~Ter1)@'sliding rate'

#RULE 5: RNAP fell off from DNA
%var: 'rnap dna felloff rate' 0.0001
%var: 'rnap dna high felloff rate' 10
RNAP(dna!1), DNA(binding!1) -> RNAP(dna), DNA(binding)@'rnap dna felloff rate'
RNAP(dna!1), DNA(binding!1, type~Ter1) -> RNAP(dna), DNA(binding, type~Ter1)@'rnap dna high felloff rate'


#RULE 6: RNAP fell off from RNA
%var: 'rnap rna felloff rate' 0.0001
%var: 'rnap rna high felloff rate' 10
RNAP(rna!1), RNA(ds!1) -> RNAP(rna), RNA(ds)@'rnap rna felloff rate'
RNAP(rna!1), RNA(ds!1,type~Ter1) -> RNAP(rna), RNA(ds,type~Ter1)@'rnap rna high felloff rate'


#RULE 7: Translation initiation
%var: 'ribosome binding rate' 0.00001
'RBS1_Ribosome binding' RNA(binding,type~RBS1), Ribosome(rna) -> RNA(binding!1, type~RBS1), Ribosome(rna!1) @ 'ribosome binding rate'
'RBS2_Ribosome binding' RNA(binding,type~RBS2), Ribosome(rna) -> RNA(binding!1, type~RBS2), Ribosome(rna!1) @ 'ribosome binding rate'
#TODO 'RBS_Ribosome binding' RNA(binding,type~RBS3), Ribosome(rna) -> RNA(binding!1, type~RBS3), Ribosome(rna!1) @ 'ribosome binding rate'

#RULE 8: Translation sliding
%var: 'translation sliding rate' 1
RNA(binding!1, ds!2), Ribosome(rna!1), RNA (binding,us!2, type~RBS1) ->RNA(binding, ds!2), Ribosome(rna!1), RNA (binding!1,us!2, type~RBS1) @'translation sliding rate'
RNA(binding!1, ds!2), Ribosome(rna!1), RNA (binding,us!2, type~RBS2) ->RNA(binding, ds!2), Ribosome(rna!1), RNA (binding!1,us!2, type~RBS2) @'translation sliding rate'
RNA(binding!1, ds!2), Ribosome(rna!1), RNA (binding,us!2, type~Ter1) ->RNA(binding, ds!2), Ribosome(rna!1), RNA (binding!1,us!2, type~Ter1) @'translation sliding rate'

#RULE 9: Translation
RNA(binding!1, ds!2), Ribosome(rna!1, protein), RNA (binding,us!2, type~spaK) ->RNA(binding, ds!2), Ribosome(rna!1, protein!3), RNA (binding!1,us!2, type~spaK), P(binding!3, type~spaK) @'translation sliding rate'
RNA(binding!1, ds!2), Ribosome(rna!1, protein), RNA (binding,us!2, type~spaR) ->RNA(binding, ds!2), Ribosome(rna!1, protein!3), RNA (binding!1,us!2, type~spaR), P(binding!3, type~spaR) @'translation sliding rate'

#RULE 10: Translation chain
RNA(binding!1, ds!2, type~spaK), Ribosome(rna!1, protein!3), RNA (binding,us!2, type~RBS2), P(binding!3, type~spaK) -> RNA(binding, ds!2, type~spaK), Ribosome(rna!1, protein!3), RNA (binding!1,us!2, type~RBS2), P(ds!4, binding, type~spaK), P(type~RBS2, us!4, binding!3) @'translation sliding rate'
RNA(binding!1, ds!2, type~RBS2), Ribosome(rna!1, protein!3), RNA (binding,us!2, type~spaR), P(binding!3, type~RBS2) -> RNA(binding, ds!2, type~RBS2), Ribosome(rna!1, protein!3), RNA (binding!1,us!2, type~spaR), P(ds!4, binding, type~RBS2), P(type~spaR, us!4, binding!3) @'translation sliding rate'
RNA(binding!1, ds!2, type~spaR), Ribosome(rna!1, protein!3), RNA (binding,us!2, type~Ter1), P(binding!3, type~spaR) -> RNA(binding, ds!2, type~spaR), Ribosome(rna!1, protein!3), RNA (binding!1,us!2, type~Ter1), P(ds!4, binding, type~spaR), P(type~Ter1, us!4, binding!3) @'translation sliding rate'


#RULE 11: Ribosome rna fell off
%var: 'ribosome rna fell off low rate' 0.001
%var: 'ribosome rna fell off high rate' 10
Ribosome(rna!1), RNA (binding!1, type~pSpaRKtss) -> Ribosome(rna), RNA (binding, type~pSpaRKtss) @'ribosome rna fell off low rate'
Ribosome(rna!1), RNA (binding!1, type~RBS1) -> Ribosome(rna), RNA (binding, type~RBS1) @'ribosome rna fell off low rate'
Ribosome(rna!1), RNA (binding!1, type~spaK) -> Ribosome(rna), RNA (binding, type~spaK) @'ribosome rna fell off high rate'
Ribosome(rna!1), RNA (binding!1, type~RBS2) -> Ribosome(rna), RNA (binding, type~RBS2) @'ribosome rna fell off low rate'
Ribosome(rna!1), RNA (binding!1, type~spaR) -> Ribosome(rna), RNA (binding, type~spaR) @'ribosome rna fell off high rate'
Ribosome(rna!1), RNA (binding!1, type~Ter1) -> Ribosome(rna), RNA (binding, type~Ter1) @'ribosome rna fell off low rate'


#RULE 12: Ribosome protein fell off
%var: 'ribosome protein fell off rate' 0.1
Ribosome(protein!3), P(binding!3) -> Ribosome(protein), P(binding)@'ribosome protein fell off rate'

#RULE 13:
%var: 'protease binding rate' 0.00005
P(deg),Protease(binding) -> P(deg!1), Protease(binding!1) @'protease binding rate'


#RULE 14:
%var: 'degradation rate' 0.01
%var: 'slide and degratation rate' 0.01
%var: 'deg sliding rate' 0.01

#Degrade a single domain protein
P(us,ds,deg!1), Protease(binding!1) -> Protease(binding) @'degradation rate'
#Degrade a multiple domain protein, slide and degrade
P(us!2,ds,deg!1), Protease(binding!1), P(ds!2, deg)-> Protease(binding!1), P(ds,deg!1), SlideDegRule()  @'slide and degratation rate'
P(ds!2,deg!1), Protease(binding!1), P(us!2, deg)-> P(ds!2,deg), Protease(binding!1), P(us!2, deg!1) @'deg sliding rate'


#RULE 15: RNADeg-RNA binding-unbinding
%var: 'mrna deg binding rate' 0.000005
RNA(deg),RNADeg(binding) -> RNA(deg!1), RNADeg(binding!1) @'mrna deg binding rate'

#RULE 16: Sliding and degradation
%var: 'mrna degratation rate' 0.01
%var: 'mrna slide and degratation rate' 0.02
%var: 'mrna deg sliding rate' 0.02

#Degrade a single domain protein
RNA(us,ds,deg!1), RNADeg(binding!1) -> RNADeg(binding), RNADegRule() @'mrna degratation rate'
#Degrade a multiple domain rna, slide and degrade
RNA(us!2,ds,deg!1), RNADeg(binding!1), RNA(ds!2, deg)-> RNADeg(binding!1), RNA(ds,deg!1)  @'mrna slide and degratation rate'
RNA(ds!2,deg!1), RNADeg(binding!1), RNA(us!2, deg)-> RNA(ds!2,deg), RNADeg(binding!1), RNA(us!2, deg!1) @'mrna deg sliding rate'


